using extension auth;

module default {
  scalar type Role extending enum<admin, user>;

  global current_user := (
    assert_single((
      select User
      filter (global ext::auth::ClientTokenIdentity) in .identities
    ))
  );

  type User {
    name: str;
    email: str {
      constraint exclusive;
    };
  
    userRole: Role {
      default := "user";
    };

    created: datetime {
      rewrite insert using (datetime_of_statement());
    }
    updated: datetime {
      rewrite insert using (datetime_of_statement());
      rewrite update using (datetime_of_statement());
    }

    # one-to-many relationship
    required multi identities: ext::auth::Identity {
      constraint exclusive;
    };

    access policy current_user_has_full_access
      allow all
      using (.id ?= global current_user.id);
    access policy admin_has_full_access
      allow all
      using (global current_user.userRole ?= Role.admin);
    access policy everyone_select_insert_update_only
      allow select, insert, update;
  }

  type Item {
    required name: str;
    required created_by: User {
      default := global current_user;
    }

    created: datetime {
      rewrite insert using (datetime_of_statement());
    }
    updated: datetime {
      rewrite insert using (datetime_of_statement());
      rewrite update using (datetime_of_statement());
    }

    access policy admin_has_full_access
      allow all
      using (global current_user.userRole ?= Role.admin);
    access policy creator_has_full_access
      allow all
      using (.created_by ?= global current_user);
    access policy others_read_only
      allow select;
  }
}